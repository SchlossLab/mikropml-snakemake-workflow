import os


containerized: "docker://kellysovacool/mikropml:latest"

configfile: "config/default.yml"


MEM_PER_GB = 1024

dataset = config["dataset_name"]
ncores = config["ncores"]
ml_methods = config["ml_methods"]
kfold = config["kfold"]
outcome_colname = config["outcome_colname"]

nseeds = config["nseeds"]
start_seed = 100
seeds = range(start_seed, start_seed + nseeds)

hyperparams = config["hyperparams"] if "hyperparams" in config else None
find_feature_importance = config["find_feature_importance"]


include: "rules/learn.smk"
include: "rules/combine.smk"
include: "rules/plot.smk"
include: "rules/example-report.smk"


rule targets:
    input:
        f"report_{dataset}.md",


rule render_report:
    input:
        R="workflow/scripts/render.R",
        Rmd="report.Rmd",
        perf_plot=rules.plot_performance.output.plot,
        feat_plot="figures/{dataset}/feature_importance.png",
        hp_plot=expand(
            "figures/{{dataset}}/hp_performance_{method}.png", method=ml_methods
        ),
        bench_plot=rules.plot_benchmarks.output.plot,
    output:
        doc="report_{dataset}.md",
    log:
        "log/{dataset}/render_report.txt",
    params:
        dataset=dataset,
        nseeds=nseeds,
        ml_methods=ml_methods,
        ncores=ncores,
        kfold=kfold,
    conda:
        "envs/mikropml.yml"
    script:
        "scripts/render.R"
